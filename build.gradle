buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
    classpath 'com.stanfy.mattock:gradle-plugin:1.0'
  }
}

apply plugin : 'java'
apply plugin : 'mattock'
apply plugin : 'nexus'

group = 'com.stanfy'
version = '0.1.8-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.google.code.gson:gson:2.2.2'
  compile 'xmlpull:xmlpull:1.1.3.1'
  
  testCompile 'junit:junit:4.11'
  testCompile 'net.sf.kxml:kxml2:2.3.0'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  testCompile 'org.easytesting:fest-reflect:1.4.1'
}

[tasks["compileJava"], tasks["compileTestJava"]].each {
  it.configure {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
  }
}

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.java
}

modifyPom {
  project {
    name 'Gson Xml'
    description 'Java library for XML deserialization'
    url 'https://github.com/stanfy/gson-xml'
    inceptionYear '2013'

    scm {
      url 'https://github.com/stanfy/gson-xml'
      connection 'scm:git:git@github.com:stanfy/gson-xml.git'
      developerConnection 'scm:git:git@github.com:stanfy/gson-xml.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'rmazur'
        name 'Roman Mazur'
        email 'rmazur@stanfy.com.ua'
      }
    }
  }
}

mattock {
  allDevices true
  ignoreMissingDevices true
}
